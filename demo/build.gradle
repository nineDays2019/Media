apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

def dev_suffix = "d"
def app_name_release = "æŽä¸€æ¡çš„æ‚è´§é“º"
def app_name_debug = "ðŸ²ðŸ¢"

static def currentSecond() {
    return new Date().format("ss", TimeZone.getTimeZone("GMT+8"))
}

static def currentTime() {
    return new Date().format("yyyy_MM_dd", TimeZone.getTimeZone("GMT+8"))
}

static def getOSName() {
    return "uname".execute().text.trim()
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.juhezi.media"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            buildConfigField("String", "CMD", "\"\"")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue("string", "app_name", app_name_release)
            applicationVariants.all { variant ->
                if (variant.buildType.name == 'release') {
                    variant.outputs.all {
                        if (outputFileName != null && outputFileName.endsWith('.apk')) {
                            def fileName = "${app_name_release}_v${defaultConfig.versionName}_${currentTime()}_${variant.flavorName}.apk"
                            outputFileName = fileName
                        }
                    }
                }
            }
        }
        debug {
            String tempCmd = ""
            if (getProject().hasProperty('cmd')) {
                tempCmd = cmd
                println("cmd is [${tempCmd}]")
            }
            buildConfigField("String", "CMD", "\"${tempCmd}\"")
            applicationIdSuffix ".${getOSName()}"
            resValue("string", "app_name", "${app_name_debug}${dev_suffix}${currentSecond()}${getOSName().charAt(0).toString()}")
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation project(':eternal')
    implementation project(':orange')
    implementation project(':ffmcli')
    implementation "com.airbnb.android:lottie:$rootProject.ext.lottieVersion"
}